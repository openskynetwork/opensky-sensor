# Maintainer: OpenSky Network <contact@opensky-network.org>
pkgname=('openskyd' 'openskyd-wd')
pkgver=1.0.8
pkgrel=1
url="http://www.opensky-network.org"
arch=('armv7h')
license=('custom')
makedepends=(gcc binutils make pkg-config)
checkdepends=(check)
source=("${pkgname}-${pkgver}.tar.gz" "${pkgname}.service"
  "${pkgname}-wd.service"
	"${pkgname}-start.sh" "${pkgname}.rbf"
	"BB-W-Radarcape-00A0.dtbo" "BB-B-Radarcape-00A0.dtbo");
md5sums=('SKIP'
         'e196ebef4681cd476987b127f21b346b'
         '5ef32f225e19fa044ffd2b1a9b4ba4b2'
         '8cb520bcf13ed0da3c6fa47042274df7'
         '935d531b3d125960432f6c73eab458b0'
         'fd78b4222eaf1e4f46621ddc17d554e3'
         '50ae4e63bf2e1b79bf372e77c1c6d614')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib/openskyd \
    --enable-silent-rules \
    --enable-input=uart --enable-standalone --enable-talkback \
    --enable-wd --with-systemd --with-pacman --with-check
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package_openskyd() {
  pkgdesc="RadarCape Replacement Daemon for OpenSky network"
  depends=(gcc-libs bash)
  optdepends=(rcc)
  backup=(etc/openskyd.conf)
  install=openskyd.install
  conflicts=('openskyd-wd')

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/bin/wd"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm755 "${srcdir}/${pkgname}-start.sh" "${pkgdir}/usr/bin/${pkgname}-start.sh"
  install -Dm644 "${srcdir}/${pkgname}.rbf" "${pkgdir}/usr/lib/openskyd/${pkgname}.rbf"
  install -Dm644 "${srcdir}/BB-B-Radarcape-00A0.dtbo" "${pkgdir}/usr/lib/firmware/BB-B-Radarcape-00A0.dtbo"
  install -Dm644 "${srcdir}/BB-W-Radarcape-00A0.dtbo" "${pkgdir}/usr/lib/firmware/BB-W-Radarcape-00A0.dtbo"
}

package_openskyd-wd() {
  pkgdesc="RadarCape Replacement Daemon for OpenSky network - Watchdog only"
  depends=(glibc)
  install=openskyd-wd.install
  conflicts=('openskyd')

  cd "${srcdir}/openskyd-${pkgver}"
  install -Dm755 "${srcdir}/openskyd-${pkgver}/src/wd" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/openskyd-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "${srcdir}/openskyd.rbf" "${pkgdir}/usr/lib/openskyd/openskyd.rbf"
}
