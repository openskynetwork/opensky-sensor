# Maintainer: Markus Engel <m_engel at opensky minus network dot org>
pkgname=openskyd
pkgver=1.0.3
pkgrel=1
pkgdesc="RadarCape Replacement Daemon for OpenSky network"
url="http://www.opensky-network.org"
arch=('armv7h')
license=('custom')
depends=(gcc-libs bash)
makedepends=(gcc binutils make pkg-config)
optdepends=(rcc)
checkdepends=(check)
backup=(etc/openskyd.conf)
install=${pkgname}.install
source=("${pkgname}-${pkgver}.tar.gz" "${pkgname}.service"
	"${pkgname}-start.sh" "${pkgname}.rbf"
	"BB-W-Radarcape-00A0.dtbo" "BB-B-Radarcape-00A0.dtbo");
md5sums=('SKIP'
         'e196ebef4681cd476987b127f21b346b'
         '8cb520bcf13ed0da3c6fa47042274df7'
         '8650715547a464898802302a9580cac8'
         'fd78b4222eaf1e4f46621ddc17d554e3'
         '50ae4e63bf2e1b79bf372e77c1c6d614')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib/openskyd \
    --enable-silent-rules \
    --enable-input=uart --enable-standalone --enable-talkback \
    --with-systemd --with-pacman --with-check
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/openskyd/LICENSE"
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm755 "${srcdir}/${pkgname}-start.sh" "${pkgdir}/usr/bin/${pkgname}-start.sh"
  install -Dm644 "${srcdir}/${pkgname}.rbf" "${pkgdir}/usr/lib/openskyd/${pkgname}.rbf"
  install -Dm644 "${srcdir}/BB-B-Radarcape-00A0.dtbo" "${pkgdir}/usr/lib/firmware/BB-B-Radarcape-00A0.dtbo"
  install -Dm644 "${srcdir}/BB-W-Radarcape-00A0.dtbo" "${pkgdir}/usr/lib/firmware/BB-W-Radarcape-00A0.dtbo"
}
