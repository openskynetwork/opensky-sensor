pkgname=openskyd
pkgver=0.7.3
pkgrel=1
pkgdesc="RadarCape Replacement Daemon for OpenSky network"
url="http://www.opensky-network.org"
arch=('armv7h')
license=('GPL3')
depends=(gcc-libs)
makedepends=(gcc binutils make dtc-git-patched)
conflicts=()
replaces=()
backup=(etc/openskyd.conf)
install=${pkgname}.install
source=("${pkgname}-${pkgver}.tar.gz" "${pkgname}.service" "${pkgname}-start.sh" "${pkgname}.rbf" "BB-W-Radarcape.dts")
md5sums=('SKIP'
         'e196ebef4681cd476987b127f21b346b'
         '2832b4440ffa4e305df45adf3c71c4bc'
         'f6453aa6ec4188c739ec8c6ecc5bef55'
	 '3575e02f6fce4fbe461c2064f2931f92')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib/openskyd \
    --disable-network --disable-invasive-talkback
  make
  cd "${srcdir}"
  dtc -O dtb -o BB-W-Radarcape-00A0.dtbo -b 0 -@ BB-W-Radarcape.dts
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/usr/bin/openskyd-uart" "${pkgdir}/usr/bin/openskyd"
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm755 "${srcdir}/${pkgname}-start.sh" "${pkgdir}/usr/bin/${pkgname}-start.sh"
  install -Dm644 "${srcdir}/${pkgname}.rbf" "${pkgdir}/usr/lib/openskyd/${pkgname}.rbf"
  install -Dm644 "${srcdir}/BB-W-Radarcape-00A0.dtbo" "${pkgdir}/usr/lib/firmware/BB-W-Radarcape-00A0.dtbo"
}

